//
// Simple test to make sure popup menus work with JFrames. Versions of
// the interpreter prior to 2.1.6 included JComponent tests that meant
// the owner of popup couldn't be a JFrame, JDialog, or JWindow.
//

import yoix.*.*;

JFrame f = {
    String title = "JFrame Popup Test";

    Dimension size = {
	double height = 4*72;		// 4 inches
	double width = VM.screen.width/2;
    };

    Point location = {
	double x = 2*72;		// 2 inches
	double y = 1*72;		// 1 inches
    };

    Color background = Color.gray;

    JPopupMenu popup = {
	Color background = Color.lightGray;

	Array items = {
	    new JMenuItem {
		String text = "Testing 1";

		actionPerformed(e) {
		    fprintf(stderr, "actionPerformed: e=%O\n", e);
		}
	    },
	    new JMenuItem {
		String text = "Testing 2";

		actionPerformed(e) {
		    fprintf(stderr, "actionPerformed: e=%O\n", e);
		}
	    },
	};
    };

    Array layout = {
	new JLabel {
	    String font = "Helvetica-plain-14";
	    String text = "This Is A Centered Label";
	    Color  foreground = Color.red;
	    int    alignment = CENTER;
	}, NORTH,

	new JTextArea {
	    String tag = "$_textarea";
	    /////Color  background = Color.white;
	    String text = "";
	    int    rows = 0;
	    int    columns = 0;
	    int    edit = TRUE;
	    int    scroll = AS_NEEDED;

	    String oldtext = text; // used in event handler below

	    textValueChanged(e) {
		switch(e.type) {
		case TEXTINSERT:
		    fprintf(stderr, "Inserted text: '%s'\n", substring(text, e.offset, e.offset+e.length));
		    break;
		case TEXTREMOVE:
		    fprintf(stderr, "Removed text: '%s'\n", substring(oldtext, e.offset, e.offset+e.length));
		    break;
		}
		oldtext = text;
	    }
	}, CENTER,

	new JPanel {
	    FlowLayout  layoutmanager = {
		int vgap = 72/12;
	    };

	    Array layout = {
		new JPanel {
		    GridLayout layoutmanager;

		    Array layout = {
			new JButton {
			    String text = "Update";
			    int    requestfocus = TRUE;

			    actionPerformed(e) {
				root.components.$_textarea.text += yoix.util.date() + "\n";
			    }
			},

			new JButton {
			    String text = "Clear";

			    actionPerformed(e) {
				root.components.$_textarea.text = "";
			    }
			},

			new JButton {
			    String text = "Quit";

			    actionPerformed(e) {
				exit(0);
			    }
			},
		    };
		},
	    };
	}, SOUTH,

	//
	// Side padding - just for looks.
	//
	new JCanvas {
	    Dimension size = {
		int width = 72/8;
		int height = 72/8;
	    };
	}, EAST,

	new JCanvas {
	    Dimension size = {
		int width = 72/8;
		int height = 72/8;
	    };
	}, WEST,
    };

    mousePressed(e) {
	if (e.popuptrigger) {
	    popup.location = e.location;
	    popup.visible = TRUE;
	}
    }

    mouseReleased(e) {
	if (e.popuptrigger) {
	    popup.location = e.location;
	    popup.visible = TRUE;
	}
    }
};

f.visible = TRUE;

