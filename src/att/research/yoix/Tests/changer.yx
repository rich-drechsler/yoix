import yoix.*.*;

JFrame screen = {
    Dimension size = NULL;
    int       visible = TRUE;

    Array layout = {
	new JTable {
	    String tag = "$_table";
	    int reorder = TRUE;
	    int resize = TRUE;
	    int scroll = AS_NEEDED;
	    int edit = 0;
	    Array headers = { "Region", "Median", "% of US", "Selected" };
	    Array types = { STRING_TYPE, MONEY_TYPE, PERCENT_TYPE, BOOLEAN_TYPE };
	    String values = "US|44473|1|1\nNH|57352|1.28959|0\n"
		+ "MI|44476|1.00007|0\nWV|32589|0.73278|0\n";
	    int resizemode = AUTO_RESIZE_OFF;
	    Color background = Color.white;


	    invocationChange(e) {
		switch(e.change) {
		case "resize":
		    if (e.viewColumn >= 0) {
			root.SyncSizes(this, root.components.$_filter);
		    }
		    break;
		case "drag":
		    root.components.$_filter.action(MOVE_COLUMN, e.fromViewColumn, e.toViewColumn);
		    break;
		}
	    }

	    int adjusting = FALSE;
	    adjustmentValueChanged(e) {

		if (e.orientation == HORIZONTAL) {
		    adjusting = TRUE;

		    if (!root.components.$_filter.adjusting)
			root.components.$_filter.origin = new Point { double x = origin.x; int y = 0; };
		    adjusting = FALSE;
		}
	    }
	},
	CENTER,
	new JTable {
	    String tag = "$_filter";
	    int reorder = 0;
	    int rows = 1;
	    int resize = 0;
	    int scrollinit = NONE;
	    int scrollvert = HORIZONTAL_NEVER|VERTICAL_ALWAYS;
	    int scrollmode = scrollinit;
	    int scroll = scrollmode;
	    int resizemode = AUTO_RESIZE_OFF;
	    int rowheightadjustment = -1;
	    int edit = TRUE;
	    Color background = Color.white;
	    Color selectionbackground = Color.white;

	    String outputfilter = "\t";

	    Array types = {
		STRING_TYPE, STRING_TYPE, STRING_TYPE, STRING_TYPE,
	    };
	    String text = "A|B|C|D";

	    int adjusting = FALSE;
	    adjustmentValueChanged(e) {

		if (e.orientation == HORIZONTAL) {
		    adjusting = TRUE;

		    if (!root.components.$_table.adjusting)
			root.components.$_table.origin = new Point { double x = origin.x; int y = 0; };
		    adjusting = FALSE;
		}
	    }
	},
	SOUTH,
    };

    SyncSizes(JTable master, JTable slave) {
	if (master.size.height > master.viewport.height) {
	    if (slave.scrollmode == slave.scrollinit) {
		slave.scrollmode = slave.scrollvert;
		slave.scroll = slave.scrollvert;
	    }
	} else {
	    if (slave.scrollmode == slave.scrollvert) {
		slave.scrollmode = slave.scrollinit;
		slave.scroll = slave.scrollinit;
	    }
	}
	for(n = 0; n < master.width; n++) {
	    w = master.action(GET_COLUMN_FIELD, n, "width");
	    v = master.action(GET_COLUMN_VIEW_INDEX, n);
	    m = slave.action(GET_COLUMN_DATA_INDEX, v);
	    slave.action(SET_COLUMN_FIELD, m, "width", w);
	}
    }
};
