import yoix.*.*;

BuildYoixLogoImage(double height, Color color, int addshadow) {
    GenImage(double height, Color color, Font imagefont, double scale) {
	Image yoixlogo = {
	    int type = TYPE_RGB_ALPHA;
	    Color imgcolor = color;
	    double scale = scale;
	    Font imagefont = imagefont;
	    Font regfont = imagefont.scalefont(0.5, 0.5);

	    Graphics graphics = {
		Font font = imagefont;
		int textantialiasing = TRUE;
	    };
	    double ywd = stringWidth(graphics.font, "Y");
	    Dimension size = {
		double height = height;
		double width = ywd * 6.2;
	    };
	    double owd = stringWidth(graphics.font, "o");
	    double iwd = stringWidth(graphics.font, "i");
	    double xwd = stringWidth(graphics.font, "x");
	    double rwd = stringWidth(regfont, "\xAE");
	    double spr = 0.33;
	    if (rwd > (1.2 * iwd)) {
		regfont = regfont.scalefont((1.2 * iwd)/rwd, (1.2 * iwd)/rwd);
		spr *= (rwd / (1.2 * iwd));
	    }
	    ywd /= 2.0;
	    ywd += 0.75 * iwd;
	    paint(Rectangle r) {
		double alpha = 1.0;
		double alpha2 = 0.3333;
		int    limit = 12;

		graphics {
		    gsave();
		    erasedrawable(0.0);
		    rectclip(r);
		    setrgbcolor(imgcolor.red, imgcolor.green, imgcolor.blue);
		    translate(44 * this.scale, 44 * this.scale);
		    for(n=0; n<limit; n++) {
			moveto(0.0, 0.0);
			setfont(this.imagefont);
			show("Y", alpha);
			if (n == 0) {
			    moveto(ywd, 0.0);
			    show("o", alpha);
			    moveto(ywd + owd + 0.25 * iwd, 0.0);
			    show("i", alpha);
			    moveto(ywd + owd + iwd + 0.25 * iwd, 0.0);
			    show("x", alpha);
			    moveto(ywd + owd + iwd + xwd + 0.6 * iwd, -this.imagefont.height * this.spr);
			    setfont(this.regfont);
			    show("\xAE", alpha);
			    alpha = alpha2;
			}
			alpha *= 0.75;
			rotate(30);
		    } 
		    grestore();
		}
	    }
	};

	return(yoixlogo);
    }

    Font basefont = {
	String name = "ClearviewATT-plain-48";
    };
    double scale = height / 90.0;
    Font imagefont = basefont.scalefont(scale, scale);

    Image imgage;
    if (addshadow) {
	Image logo = GenImage(height, color, imagefont, scale);
	image = new Image {
	    int type = TYPE_RGB_ALPHA;
	    Image source = logo;
	    Image img = logo;
	    Image shadow = new Image {
		int type = TYPE_RGB_ALPHA;
		Image source = img;
		Array kernel = new Array[100];
		Pointer ptr;
		for(ptr in kernel) *ptr = 0.0055;
		paint() {
		    convolve(kernel);
		}
	    };
	    paint(Rectangle r) {
		graphics {
		    gsave();
		    moveto(0, 0);
		    showimage(this.img);
		    moveto(this.img.size.height * 0.005, this.img.size.height * 0.02);
		    showimage(this.shadow);
		    grestore();
		}
	    }
	};
    } else {
	image = GenImage(height, color, imagefont, scale);
    }

    return(image);
}

//
// first arg is height of logo, second arg is either 0/1 to indicate
// shadow/no-shadow, any other value is considered a filename into which
// file the generated image will be written.
//

double sz = (argc > 1) ? atof(argv[1]) : 270;
int shdw = 1;
int print = 0;
if (argc > 2) {
    if (argv[2] =~ "^[01]$") {
	shdw = atoi(argv[2]);
    } else {
	print = 1;
    }
}
	

Image yoixlogo = BuildYoixLogoImage(sz, Color.black, (sz >= 72) && shdw);

if (print) {
    Stream output;

    if ((output = open(argv[2], "w", FILE)) != NULL) {
	encodeImage(yoixlogo, "png", output);
	close(output);
    }
} else {
    JFrame jf = {
	int visible = TRUE;
	Dimension size = NULL;
	Array layout = {
	    new JPanel {
		Dimension preferredsize = {
		    double width = yoixlogo.size.width;
		    double height = yoixlogo.size.height;
		};
		Color background = Color.white;
		Image backgroundimage = yoixlogo;
		int backgroundhints = SCALE_NONE;
	    },
	};
    };
}
