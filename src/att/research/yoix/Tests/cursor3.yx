//
// Another simple custom cursor example - this one makes no attempt at
// defining a cursor that other components can use.
//

import yoix.*.*;

JFrame frame = {
    Image cursor = {
	int type = TYPE_RGBA;

	Dimension size = {
	    double width = 72/3;
	    double height = 72/3;
	 };

	Graphics graphics = {
	    int rendering = 1;		// probably no help
	    int antialiasing = 1;
	};

	paint(Rectangle r) {
	    Rectangle bbox;
	    double    thickness;
	    double    radius;
	    double    alpha;
	    double    cx;
	    double    cy;

	    graphics {
		gsave();
		initclip();
		clippath();
		bbox = pathbbox();
		newpath();

		cx = bbox.width/2;
		cy = bbox.height/2;
		thickness = cx/4.0;
		radius = cx - thickness - 1;
		alpha = 180*atan2(thickness/2, radius)/PI;

		translate(cx, cy);
		rotate(45);

		moveto(0, thickness/2);
		arc(0, 0, radius, alpha, 180 - alpha);
		closepath();

		moveto(0, -thickness/2);
		arc(0, 0, radius, 180 + alpha, 360 - alpha);
		closepath();

		moveto(radius + thickness, 0);
		arc(0, 0, radius + thickness, 0, 360);
		closepath();

		gsave();
		setrgbcolor(1, 0, 0);
		eofill();
		grestore();

		setlinewidth(1);
		setrgbcolor(1, 1, 1);
		stroke();
		grestore();

		hotspot = new Point {
		    double x = cx;
		    double y = cy;
		};
	    }
	}
    };
};

frame.visible = TRUE;
