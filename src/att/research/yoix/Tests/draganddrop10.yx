//
// Finally a combination of the last two examples.
//

import yoix.*.*;

PaintScrew(double angle) {
    Image image = {
	int type = TYPE_RGBA;

	Dimension size = {
	    double width = 72/3;
	    double height = 72/3;
	 };

	Graphics graphics = {
	    int rendering = 1;		// probably no help
	    int antialiasing = 1;
	};

	double angle = angle;

	paint(Rectangle r) {
	    Rectangle bbox;
	    double    thickness;
	    double    radius;
	    double    alpha;
	    double    cx;
	    double    cy;

	    graphics {
		gsave();
		initclip();
		clippath();
		bbox = pathbbox();
		newpath();

		cx = bbox.width/2;
		cy = bbox.height/2;
		thickness = cx/4.0;
		radius = cx - thickness - 1;
		alpha = 180*atan2(thickness/2, radius)/PI;

		translate(cx, cy);
		rotate(angle);

		moveto(0, thickness/2);
		arc(0, 0, radius, alpha, 180 - alpha);
		closepath();

		moveto(0, -thickness/2);
		arc(0, 0, radius, 180 + alpha, 360 - alpha);
		closepath();

		moveto(radius + thickness, 0);
		arc(0, 0, radius + thickness, 0, 360);
		closepath();

		gsave();
		setrgbcolor(0, 0, 1);
		eofill();
		grestore();

		setlinewidth(1);
		setrgbcolor(1, 1, 0);
		stroke();
		grestore();

		hotspot = new Point {
		    double x = cx;
		    double y = cy;
		};
	    }
	}
    };

    return(image);
}

Array names[30];

for (n = 0; n < names@length; n++) {
    names[n] = "SCREW_" + toString(n);
    addCursor(names[n], PaintScrew(n*180/names@length));
}

JFrame frame1 = {
    String title = "Frame 1";
    Object transferhandler = NULL;	// just in case
    Color  background = Color.lightGray;
    int    next = 1;
    int    incr = 1;

    Dimension size = {
	double width = VM.screen.width/3;
	double height = VM.screen.height/3;
    };

    Point location = {
	double x = 72/2;
	double y = (VM.screen.height - size.height)/2;
    };

    dragGestureRecognized(DragGestureEvent e) {
	//
	// We have to return something that's not NULL, even though
	// this example doesn't transfer anything, otherwise dragging
	// won't start.
	//
	e.cursor = names[next++ % names@length];
	return("");		// bogus return
    }

    dragEnter(DropTargetEvent e) {
	return(TRUE);
    }

    dragOver(DragSourceEvent e) {
	e.cursor = names[next];
	if ((next += incr) < 0)
	    next = names@length - 1;
	else if (next >= names@length)
	    next = 0;
    }
};

JFrame frame2 = {
    String title = "Frame 2";
    Object transferhandler = NULL;	// just in case
    Color  background = Color.darkGray;
    int    next = 1;
    int    incr = -1;

    Dimension size = {
	double width = VM.screen.width/3;
	double height = VM.screen.height/3;
    };

    Point location = {
	double x = VM.screen.width - size.width - 72/2;
	double y = (VM.screen.height - size.height)/2;
    };

    dragGestureRecognized(DragGestureEvent e) {
	//
	// We have to return something that's not NULL, even though
	// this example doesn't transfer anything, otherwise dragging
	// won't start.
	//
	e.cursor = names[next++ % names@length];
	return("");		// bogus return
    }

    dragOver(Object e) {
	//
	// The argument will be a DropTargetEvent or DragSourceEvent.
	// We accept by always returning TRUE (the return value is
	// ignored when e is a DragSourceEvent) and we change the
	// when e is a DragSourceEvent.
	//
	if (e instanceof DragSourceEvent) {
	    e.cursor = names[next];
	    if ((next += incr) < 0)
		next = names@length - 1;
	    else if (next >= names@length)
		next = 0;
	}
	return(TRUE);
    }
};

frame1.visible = TRUE;
frame2.visible = TRUE;
