//
// This example animates a cursor using mouseDragged(), addCursor(),
// and a slightly modified cursor from an earlier example. There's
// a drag and drop example that accomplishes the same thing, but it
// uses dragMouseMoved() - see if you notice the main difference.
//

import yoix.*.*;

PaintScrew(double angle) {
    Image image = {
	int type = TYPE_RGBA;

	Dimension size = {
	    double width = 72/3;
	    double height = 72/3;
	 };

	Graphics graphics = {
	    int rendering = 1;		// probably no help
	    int antialiasing = 1;
	};

	double angle = angle;

	paint(Rectangle r) {
	    Rectangle bbox;
	    double    thickness;
	    double    radius;
	    double    alpha;
	    double    cx;
	    double    cy;

	    graphics {
		gsave();
		initclip();
		clippath();
		bbox = pathbbox();
		newpath();

		cx = bbox.width/2;
		cy = bbox.height/2;
		thickness = cx/4.0;
		radius = cx - thickness - 1;
		alpha = 180*atan2(thickness/2, radius)/PI;

		translate(cx, cy);
		rotate(angle);

		moveto(0, thickness/2);
		arc(0, 0, radius, alpha, 180 - alpha);
		closepath();

		moveto(0, -thickness/2);
		arc(0, 0, radius, 180 + alpha, 360 - alpha);
		closepath();

		moveto(radius + thickness, 0);
		arc(0, 0, radius + thickness, 0, 360);
		closepath();

		gsave();
		setrgbcolor(0, 0, 1);
		eofill();
		grestore();

		setlinewidth(1);
		setrgbcolor(1, 1, 0);
		stroke();
		grestore();

		hotspot = new Point {
		    double x = cx;
		    double y = cy;
		};
	    }
	}
    };

    return(image);
}

Array names[30];

for (n = 0; n < names@length; n++) {
    names[n] = "SCREW_" + toString(n);
    addCursor(names[n], PaintScrew(n*180/names@length));
}

JFrame frame = {
    String cursor = names[0];
    int    next = 1;

    mouseDragged(e) {
	cursor = names[next++ % names@length];
    }
};

frame.visible = TRUE;
