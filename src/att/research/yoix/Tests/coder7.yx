import yoix.*.*;

int n;

Array textvals = {
    "Now is the time",
    "for all good men",
    "to come to the aid",
    "of their country.",
};
String step1_out = "F211F3E1A792E7FFB230415D8CAAE22EA3C9B5FE4DE9399E3A3211A67A7FC5778095E6B332F17941CA6BB4A237B0825433BE014F0D3A0ED18938ADBF04C306D4060A98BF67E0C4ED60A4FAA1AB7A6F79D9F1FF1B56E0F828";

String step2_out = "";

String output;
String NL = getProperty("line.separator");

Cipher c = {
    String specification = "DES";
    int opmode = ENCRYPT_MODE;
    String initializer = "AF449F90307B46828DF6BB3D57D248C791F92985E9B17F5557B3F32685C85827C281908F0DACF666";
};

c.opmode = ENCRYPT_MODE;
StringStream ss1 = {
    int mode = WRITE;
    int open = TRUE;
    Cipher cipher = c;
    int filters = HEXCODER|GZIP;
};

for (n=0; n<textvals@length; n++) {
    ss1.nextline = textvals[n];
    step2_out += textvals[n] + NL;
}
close(ss1);

stdout.nextline = "=========== Step 1 ===========";

if (ss1.name === step1_out)
    stdout.nextline = "Step 1: coding succeeded.";
else stdout.nextline = "Step 1: coding failed.";
stdout.nextline = "==============================";

stdout.nextline = ss1.name;

c.opmode = DECRYPT_MODE;

StringStream ss2 = {
    int mode = READ;
    int open = TRUE;
    String name = ss1.name;
    Cipher cipher = c;
    int filters = HEXCODER|GZIP;
};

output = ss2.nextbuf;

stdout.nextline = "=========== Step 2 ===========";

if (output === step2_out)
    stdout.nextline = "Step 2: de-coding succeeded.";
else stdout.nextline = "Step 2: de-coding failed.";
stdout.nextline = "==============================";

stdout.nextbuf = output;
