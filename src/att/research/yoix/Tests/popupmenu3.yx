//
// Swing version of the old AWT test file that shares a single PopupMenu.
//

import yoix.*.*;

JPopupMenu popupmenu = {
    String text = "Test Popup Menu";

    Menu items = {
        "Line 1", "1",
        "Line 2", "2",
        "More...", new Menu {
            "Line 3", "3",
            "Line 4", "4", TRUE,
        },
        "-", NULL,
        "Exit", "exit",
    };
};

mouseEventHandler(e) {
    if (e.popuptrigger) {
        global.popupmenu.location = e.location;
        this.popup = global.popupmenu;
    }
}

JFrame f = {
    GridLayout layoutmanager;

    Array layout = {
        new JLabel {
            String text = "A Label";
            Color  background = Color.red;
            int    alignment = CENTER;

            Function mousePressed = mouseEventHandler;
            Function mouseReleased = mouseEventHandler;

            actionPerformed(e) {
                printf("Received: %O\n", e);
                if (strcmp(e.command, "exit") == 0)
                    exit(0);
            }
        },

        new JButton {
            String text = "A Button";
            Color  background = Color.magenta;
            int    alignment = CENTER;

            Function mousePressed = mouseEventHandler;
            Function mouseReleased = mouseEventHandler;

            itemStateChanged(e) {
                printf("Received: %O\n", e);
            }
        },
    };
};

f.visible = TRUE;

