//
// An example that tests AWT TextField backgrounds. Yoix versions
// newer than 0.9.2 try to compensate for inconsistencies in the
// background color of AWT TextFields (and TextAreas).
//

import yoix.*.*;

VM.screen.background = Color.gray;

Frame screen = {
    Dimension size = NULL;
    String    font = "Helvetica-bold-12";
    ////Color background = yoix.awt.Color.gray;
    ////Color foreground = yoix.awt.Color.black;
    int       initialized = FALSE;

    GridBagLayout layoutmanager;

    Array layout = {
	//
	// Padding - lower left corner
	//
	new Canvas {
	    Color background = yoix.awt.Color.lightGray;
	    Dimension size = {
		int width = 36;
		int height = 36;
	    };
	},

	new GridBagConstraints {
	    int gridx = 0;
	    int gridy = 0;
	},

	//
	// Input fields
	//
	new Panel {
	    String tag = "$_panel";

	    GridLayout layoutmanager = {
		int rows = 0;
		int columns = 2;
	    };

	    Array layout = {
		new Label {
		    String text = "Username";
		    int    alignment = RIGHT;
		},

		new TextField {
		    String tag = "$_username";
		    int    columns = 12;

		    focusGained(e) {
			printf("%s got the focus\n", tag);
		    }
		},

		new Label {
		    String text = "Password";
		    int    alignment = RIGHT;
		},

		new TextField {
		    String tag = "$_password";
		    int    columns = 12;
		    int    echo = '*';

		    focusGained(e) {
			printf("%s got the focus\n", tag);
		    }
		},
	    };
	},

	new GridBagConstraints {
	    int gridx = 2;
	    int gridy = 5;
	    int gridwidth = 5;
	    int anchor = CENTER;
	    int weightx = 100;
	    int weighty = 100;

	    Insets insets = {
		int right = 72;		// positioning kludge??
	    };
	},

	//
	// Padding - lower right corner
	//
	new Canvas {
	    Color background = yoix.awt.Color.lightGray;

	    Dimension size = {
		int width = 36;
		int height = 36;
	    };
	},

	new GridBagConstraints {
	    int gridx = 10;
	    int gridy = 10;
	 },
    };

    ////////////////////////////
    //
    // Support functions
    //
    ////////////////////////////

    ShowScreen(Dimension size, Point location) {
	this.size = size;

	if (!initialized) {
	    if (location == NULL) {
		location = new Point;
		if (parent == NULL) {
		    location.x = (VM.screen.width - this.size.width)/2;
		    location.y = (VM.screen.height - this.size.height)/2 - 72;
		} else {
		    location.x = parent.location.x + (parent.size.width - this.size.width)/2;
		    location.y = parent.location.y + parent.size.height - 72/4;
		}
	    }
	    initialized = TRUE;
	}
	this.location = location;
	visible = TRUE;
    }
};

screen.ShowScreen(NULL, NULL);
sleep(3);

//
// Explicitly set the Frames's background color - the background of the
// two TextFields should also change.
//
screen.background = Color.gray;
sleep(3);

//
// Change the username TextField so it's readonly - you probably should
// only notice a permanent change is you're using 1.1.3 (which is broken
// but will eventually be fixed - see java bug 4403599).
//
screen.components.$_username.edit = FALSE;
sleep(3);

//
// Set the background of the password TextField to green.
//
screen.components.$_password.background = Color.green;
sleep(3);

//
// Set it back to NULL to test new java code in YoixBodyComponent.java.
//
screen.components.$_password.background = NULL;
sleep(3);

//
// Set the background of the password TextField back to green so we can
// another test of new Java code.
//
screen.components.$_password.background = Color.green;
sleep(3);

//
// Set the screen's background color to NULL.
//
screen.background = NULL;
sleep(3);

//
// Set the background color in $_panel.
//
screen.components.$_panel.background = Color.red;
sleep(3);

//
// Set it back to NULL - another test new Java code.
//
screen.components.$_password.background = NULL;
sleep(3);

//
// Set the background color in $_panel.
//
screen.components.$_panel.background = NULL;
sleep(3);

screen.visible = FALSE;

