//
// Another simple charpath() test. This one shows some text and then uses
// charpath() and pathbbox() to draw a tight fitting rectangle around the
// text that show() rendered. Remember that if you rotate the coordinate
// system pathbbox() usually will return a Rectangle that is bigger than
// you might expect. In that case stringbounds() will usually be a better
// choice, but this is supposed to be a charpath() test, so that's what
// we use.
//

import yoix.*.*;

JFrame f = {
    String text = "Now is the time for all good men...";
    Color  background = Color.black;

    Dimension size = {
	double width = 72*12;
	double height = 72*5;
    };

    Graphics graphics = {
	String font = "Lucida-bold-36";
	Color  foreground = Color.yellow;
	int    antialiasing = TRUE;
	int    textantialiasing = TRUE;
	int    fractionalmetrics = TRUE;
    };

    paint(rect) {
	graphics {
	    gsave();
	    rectclip(rect);
	    translate(18, 144);
	    moveto(0, 0);
	    show(text);
	    moveto(0, 0);
	    setrgbcolor(1, 0, 0);
	    charpath(text);
	    rectstroke(pathbbox());
	    grestore();
	}
    }
};

f.visible = TRUE;

